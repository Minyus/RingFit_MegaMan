#pragma METAINFO("ringfit_megaman", 1, 0, "Minyus")

main {

		
	// ACCEL_2_Y > 0 = Tilt to right
	if(get_val(ACCEL_2_Y) > 15.00)
		{
			// Stick_2_X = 100 -> Stick_2 Right
			set_val(STICK_2_X, 100.00);
		}
	if(get_val(ACCEL_2_Y) < -15.00)
		{
			// Stick_2_X = -100 -> Stick_2 Left
			set_val(STICK_2_X, -100.00);
		}

	if((get_prev(ACCEL_2_Y) < 0.00) && (get_val(ACCEL_2_Y) >= 0.00))
		{
			combo_run(LeftToRight);
		}

	if((get_prev(ACCEL_2_Y) >= 0.00) && (get_val(ACCEL_2_Y) < 0.00))
		{
			combo_run(RightToLeft);
		}
	
	// ACCEL_2_Z > 0 -> Tilt to backward
	if(get_val(ACCEL_2_Z) > 10.00)
		{
			// Stick_2 = -100 -> Up
			set_val(STICK_2_Y, -100.00);
		}
	if(get_val(ACCEL_2_Z) < -10.00)
		{
			// Stick_2 = 100 -> Down
			set_val(STICK_2_Y, 100.00);
		}

	// Squeeze or Pull Ring-con
	if(
		(get_val(GYRO_1_Z) < -4.00) ||
		(get_val(GYRO_1_Z) > 4.00)
		)
		{
			// BUTTON_17 (Y Button) -> Buster
			set_val(BUTTON_17, 100.00);
		}
	
	
	// Disable
	set_val(BUTTON_10, 0.00);
	set_val(BUTTON_11, 0.00);
	set_val(BUTTON_12, 0.00);
	set_val(BUTTON_13, 0.00);
	
	// ACCEL_1_Z < 2 -> Squat
	if(get_val(ACCEL_1_Z) < 2.00)
		{
			if(get_val(ACCEL_2_Z) > 10.00) combo_run(BigJump);
			else combo_run(SmallJump);
		}
}

combo LeftToRight
{
	set_val(STICK_2_X, 100.00);
	wait(10);
	set_val(STICK_2_X, 0.00);
}

combo RightToLeft
{
	set_val(STICK_2_X, -100.00);
	wait(10);
	set_val(STICK_2_X, 0.00);
}


// Button_16 (B Button) = 100 -> Jump
combo SmallJump
{
	wait(1);
	set_val(BUTTON_16, 100.00);
	wait(150);
	set_val(BUTTON_16, 0.00);
}

combo BigJump
{
	wait(1);
	set_val(BUTTON_16, 100.00);
	wait(300);
	set_val(BUTTON_16, 0.00);
}